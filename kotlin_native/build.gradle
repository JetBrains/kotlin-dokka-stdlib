apply plugin: 'com.github.jk1.tcdeps'
apply plugin: 'de.undercouch.download'
apply plugin: 'base'

//TODO: there is no direct sync between tag and K/N version :(
final String kotlin_native_sources_version = '1.1'
final String kotlin_native_sources_tag = "ec9b057e5a0fa69d1bad4d60219649a7d2ed3a1d" // "v${kotlin_native_sources_version}"

final String kotlin_native_build_configuration = 'Kotlin_KotlinNative_Master_KotlinNativeDist';
final String kotlin_native_version = "1.1-dev-4569" // "${kotlin_native_sources_version}"
final String kotlin_native_build = "1.1-dev-4569" // "${kotlin_native_version}-release-4565"

repositories {
  teamcityServer { url = 'https://buildserver.labs.intellij.net' }
}

configurations {
  kotlin_native_linux
  kotlin_native_mac
  kotlin_native_windows
}

dependencies {
  kotlin_native_linux   tc(buildTypeId: kotlin_native_build_configuration, version: kotlin_native_build, artifactPath: "kotlin-native-linux-${kotlin_native_version}.tar.gz")
  kotlin_native_mac     tc(buildTypeId: kotlin_native_build_configuration, version: kotlin_native_build, artifactPath: "kotlin-native-macos-${kotlin_native_version}.tar.gz")
  kotlin_native_windows tc(buildTypeId: kotlin_native_build_configuration, version: kotlin_native_build, artifactPath: "kotlin-native-windows-${kotlin_native_version}.zip")
}

final File kotlin_native_dir = new File(buildDir, "kotlin_native")

task extractKotlinNativeBinariesLinux(type: Sync) {
  from { tarTree(resources.gzip(configurations.kotlin_native_linux.singleFile)) }
  into new File(kotlin_native_dir, 'linux')
}

task extractKotlinNativeBinariesMac(type: Sync) {
  from { tarTree(resources.gzip(configurations.kotlin_native_mac.singleFile)) }
  into new File(kotlin_native_dir, 'mac')
}

task extractKotlinNativeBinariesWindows(type: Sync) {
  from { zipTree(configurations.kotlin_native_windows.singleFile) }
  into new File(kotlin_native_dir, 'windows')
}

final File kotlinSourcesZip = new File(buildDir, "kotlin-${kotlin_native_sources_tag}.zip")
final File kotlinTargetDir = new File(buildDir, "kotlin-native-sources")
// github strips leading 'v' from tag for archive suffix
final String kotlinSourcesSuffix = (kotlin_native_sources_tag.startsWith("v")) ? kotlin_native_sources_tag.drop(1) : kotlin_native_sources_tag
final File kotlinSources = new File(kotlinTargetDir, "kotlin-native-${kotlinSourcesSuffix}")

final String kotlinUrl = "https://github.com/JetBrains/kotlin-native/archive/${kotlin_native_sources_tag}.zip"

task downloadKotlinNativeSources(type: Download) {
  src kotlinUrl
  dest kotlinSourcesZip
  overwrite false
}

task extractKotlinNativeSources(type: Sync, dependsOn: downloadKotlinNativeSources) {
  from zipTree(kotlinSourcesZip)
  into kotlinTargetDir
}


task extractKotlinNative(dependsOn: [extractKotlinNativeSources, extractKotlinNativeBinariesLinux, extractKotlinNativeBinariesMac, extractKotlinNativeBinariesWindows]) {}

project.extensions.kotlin_native_root = kotlinSources
project.extensions.kotlin_native_bin_linux = new File(kotlin_native_dir, "linux/kotlin-native-linux-${kotlin_native_version}")
project.extensions.kotlin_native_bin_mac = new File(kotlin_native_dir, "mac/kotlin-native-mac-${kotlin_native_version}")
project.extensions.kotlin_native_bin_windows = new File(kotlin_native_dir, "windows/kotlin-native-windows-${kotlin_native_version}")

